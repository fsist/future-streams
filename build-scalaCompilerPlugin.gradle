// Shared script for enabling the scala macro paradise compiler plugin
// Credit: http://compulsiontocode.blogspot.co.il/2011/05/enable-scala-compiler-plugin-using.html

// NOTE: when changing the compiler plugin version (or adding or removing a plugin) you need to change three things:
// - The version below in the scalaCompilerPlugins dependency
// - The version of the `quasiquotes` dependency, which is an ordinary dependency listed in the appropriate modules,
//   and must match that of the plugin
// - In IDEA project properties, for each module that uses the plugin, edit the compiler plugin settings because
//   IDEA-gradle sync doesn't pick them up

configurations {
  scalaCompilerPlugins { transitive = false }
}

dependencies {
  scalaCompilerPlugins 'org.scalamacros:paradise_2.10.4:2.0.1' // This is the compiler plugin jar, not an actual dependency
}

def scalacXPlugins() {
  return ((configurations.scalaCompilerPlugins.files.collect { "\"-Xplugin:${it.path}\"".toString() }))
}

compileScala.scalaCompileOptions.additionalParameters.addAll(scalacXPlugins())

